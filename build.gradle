import org.apache.tools.ant.taskdefs.condition.Os

buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.9.0'
	}
}

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: "java"
apply plugin: 'maven'
apply plugin: 'signing'

if(project.hasProperty('ossrhUser') && project.hasProperty("release")) {
	apply plugin: "io.codearte.nexus-staging"

	nexusStaging {
		username = ossrhUser
		password = ossrhPassword
	}
}

group = 'com.elefana'
version = '0.9.21'
description = 'High-perfor­mance pattern matching algo­rithms in Java'

repositories {
	mavenLocal()
	mavenCentral()
}

sourceCompatibility = 1.6
targetCompatibility = 1.6

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

dependencies {
    compile 'org.javassist:javassist:3.22.0-GA'

	testCompile 'junit:junit:4.12'
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
      tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
      }
    }
  }

// Maven Central publication
if(project.hasProperty('ossrhUser')) {
	signing {
		sign configurations.archives
	}
	
	uploadArchives {
		repositories {
			mavenDeployer {
				// POM signature
				beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
				// Target repository
				repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
					authentication(userName: ossrhUser, password: ossrhPassword)
				}
				pom.project {
					name project.name
					description project.description
					packaging 'jar'
					url 'https://github.com/elefana/json-iterator'

					scm {
						connection 'scm:git:https://github.com/elefana/json-iterator.git'
						developerConnection 'scm:git:git@github.com:elefana/json-iterator.git'
						url 'https://github.com/elefana/json-iterator.git'
					}

					licenses {
						license {
							name 'MIT License'
							url 'https://opensource.org/licenses/MIT'
							distribution 'repo'
						}
					}

					developers {
						developer {
							id = 'tomcashman'
							name = 'Thomas Cashman'
							email = 'thomascashman404@gmail.com'
						}
					}
				}
			}
		}
	}
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4.1'
}